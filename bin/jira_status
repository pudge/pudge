#!/usr/bin/perl -CSDA
use strict;
use warnings;
use 5.018;
use utf8;

=pod

=encoding utf8

=head1 NAME

jira_status - handle status for Jira issues

=head1 SYNOPSIS

This program shows status and does transitions for Jira issues.

    $ jira_status status
    In Review 

    # autocomplete support
    $ jira_status transition Continue\ Progress

    $ jira_status status
    In Progress 


=head1 DESCRIPTION

Get the status, get the transitions, or execute a transition.

Autocomplete will populate the available transitions.

The issue status and the available transitions are cached for 30 minutes.

C<JIRA_ID> will be taken from the branch name if not supplied as an argument.


=head2 OPTIONS

=over 4

=item B<-h|--help>

Prints brief help message and exits.

=item B<-V|--version>

Prints version and exits.

=item B<-v|--verbose>

More verbose output.

=back


=head2 COMMANDS

=over 4

=item * status [JIRA_ID]

Returns the status (text, and emoji, if available).

=item * status_line [JIRA_ID]

Returns just the text of the status.

=item * short_status [JIRA_ID]

Returns just the emoji of the status (or text if there is no emoji).

=item * transition TRANSITION [JIRA_ID]

Transition the issue. Accepts a text name or numeric ID of a transition.

=item * list_transitions [JIRA_ID]

Returns JSON object of available transitions, C<{ID:NAME}>.

=item * estimate [MINUTES] [JIRA_ID]

Sets/gets the remaining estimate.

=item * short_estimate [JIRA_ID]

Returns just the emoji of the remaining estimate.

=item * jira_id [JIRA_ID]

Helper function to just return the Jira ID.

=item * completion SHELL

Helper function to just return the completion shell script, if available for the named shell. Supported shells: C<bash>.

=back


=head3 Raw Commands

=over 4

=item * get_transitions [JIRA_ID]

Get the raw JSON for available transitions for the Jira ID.

=item * get_issue [JIRA_ID]

Get the raw JSON for the Jira ID.

=item * refresh [JIRA_ID]

Refresh the issue and transitions cache for the Jira ID.

=back


=head2 SETUP

Add this to your F<.bashrc> or similar:

    export JIRA_HOST=jira.i.extrahop.com
    test -n $(which jira_status > /dev/null) && source <(jira_status completion bash)

Add this or similar, if desired, to your bash prompt:

    function _jira_status {
        jira_status=$( jira_status short_status 2>/dev/null );
        if [[ -n "$jira_status" ]]; then
            jira_estimate=$( jira_status short_estimate 2>/dev/null );
            jira_url="https://${JIRA_HOST}/browse/"$( jira_status jira_id )
            echo -ne ' '
            # link the Jira icon, and color it blue
            echo -ne '\001\e[34m\002\e]8;;'$jira_url'\e\\󰌃 \e]8;;\e\\\001\e[0m\002'
            echo -ne "$jira_status $jira_estimate "
        fi
    }

    PS1 = "${PS1}\$(_jira_status)"


=head3 Authorization

=over 4

=item * Get an Atlassian API token

Create a new token at Jira -> Profile -> Personal Access Tokens.

=item * Save token

Save your token to F<~/.jira_token> (set file mode to C<0600> or similar).

=back


=head1 REQUIREMENTS

=over 4

=item * apt packages

    sudo apt install libwww-perl libjson-xs-perl perl-doc

=item * Terminal font with NerdFont support

For use with C<short_status> and C<short_estimate> command.

=back

=cut

use Data::Dumper; $Data::Dumper::Sortkeys=1;
use File::Path 'mkpath';
use Getopt::Long;
use HTTP::Request;
use JSON::XS qw(decode_json encode_json);
use LWP::UserAgent;
use Pod::Usage;
use URI::Escape 'uri_escape';

our %STATUS = (
    'Selected'           => { icon => "️" },
    'In Progress'        => { icon => "" },
    'Blocked'            => { icon => "" },
    'Needs Information'  => { icon => "󰆆" },
    'In Review'          => { icon => "" },
    'Resolved'           => { icon => "" },
    'Closed'             => { icon => "" },
);

our $VERSION = '0.0.1';

our $TOOLS_DIR = '/opt/bin';
our $JIRA_USER = $ENV{JIRA_USER} // $ENV{USER};

my($host, $port) = ($ENV{JIRA_HOST}, '443');
# https://developer.atlassian.com/cloud/jira/platform/rest/v2/
my $base_url = "https://$host:$port/rest/api/2";
my $issue_url = "$base_url/issue/";

my($ua, $jira_token, $err);
my($sub, @args, $verbose, $version, $jira_cache);

if (init() && $sub) {
    my $res = $sub->(@args);
    say $res if defined $res;
}
exit($err // 0);


#### commands

sub status_line {
    my($jira_id) = @_;

    my $issue = get_issue($jira_id);

    return get_json($jira_id, $issue)->{fields}{status}{name} || '';
}


sub short_status {
    my($jira_id) = @_;

    my $status = status_line($jira_id);

    return $status ? $STATUS{$status} && $STATUS{$status}{icon} // $status : '';
}


sub status {
    my($jira_id) = @_;

    my $status = status_line($jira_id);
    my $short_status = short_status($jira_id);

    return $status if !$short_status || !$status || $status eq $short_status;
    return $status . ' ' . $short_status;
}


sub transition {
    my($jira_id, $transition_id, $options) = @_;

    output_error("No transition ID provided") and return unless $transition_id;

    my $transitions = list_transitions($jira_id);
    if ($transition_id !~ /^\d+$/) {
        ($transition_id) = grep { $transitions->{$_} eq $transition_id } keys %$transitions;
    }
    return unless $transition_id && $transitions->{$transition_id};

    my $post = { transition => { id => $transition_id } };
    my $post_enc = encode_json($post);
    say $post_enc if $verbose;
    my $res = $ua->post(transitions_url($jira_id), req_headers(), Content => $post_enc);

    res_error($jira_id, $res, "transitioning ID", {no_error => 1 }) and do {
        my $url = workflow_dispatch_url($jira_id, $transition_id);
        qx{open_url \Q$url\E 2>/dev/null};
        say "Transition at $url";
        local $| = 1;
        print "Press Enter to Continue: ";
        <>;
    };

    get_issue($jira_id, 1);
    get_transitions($jira_id, 1);

    return;
}


sub get_transitions {
    my($jira_id, $force) = @_;

    my $transitions = read_cache(transitions => $jira_id);
    return $transitions if $transitions && !$force;

    my $res = $ua->get(transitions_url($jira_id), req_headers());
    res_error($jira_id, $res, "getting issue transitions") and return;

    $transitions = $res->content;
    if ($transitions) {
        write_cache(transitions => $jira_id, $transitions);
    }
    return $transitions;
}


sub list_transitions {
    my($jira_id) = @_;

    my $transitions = get_transitions($jira_id);
    my %list = map { $_->{'id'}, $_->{'name'} } @{ get_json($jira_id, $transitions)->{'transitions'} || [] };

    return \%list;
}
sub _list_transitions { encode_json(&list_transitions) }


sub get_issue {
    my($jira_id, $force) = @_;

    my $issue = read_cache(issue => $jira_id);
    return $issue if $issue && !$force;

    my $res = $ua->get(issue_url($jira_id), req_headers());
    res_error($jira_id, $res, "getting issue") and return;

    $issue = $res->content;
    if ($issue) {
        write_cache(issue => $jira_id, $issue);
    }
    return $issue;
}


sub refresh {
    my($jira_id, $force) = @_;

    get_issue($jira_id, 1);
    get_transitions($jira_id, 1);

    return;
}


sub estimate {
    my($jira_id, $minutes) = @_;

    my $issue;
    if (defined $minutes) {
        output_error("Minutes must be an integer") and return unless $minutes =~ /^\d+$/;

        $issue = get_issue($jira_id, 1);
        my $originalEstimate = get_json($jira_id, $issue)->{fields}{timetracking}{originalEstimate};

        my $post = {
            fields => {
                timetracking => {
                    originalEstimate => $originalEstimate,
                    remainingEstimate => "${minutes}m"
                }
            }
        };
        my $post_enc = encode_json($post);
        say $post_enc if $verbose;

        my $url = issue_url($jira_id);
        my $res = $ua->put($url, req_headers(), Content => $post_enc);
        res_error($jira_id, $res, "estimating ID") and return;

        $issue = get_issue($jira_id, 1);
        get_transitions($jira_id, 1);
    }
    else {
        $issue = get_issue($jira_id);
    }

    my $est = get_json($jira_id, $issue)->{fields}{timeestimate};
    $est /= 60 if $est;
    return $est // '';
}


sub short_estimate {
    my($jira_id) = @_;

    my $est = int( estimate($jira_id) / 60 );
    return '' unless $est;
    my $val;
    if ($est) {
        $val = hex 'f143e';
        if ($est >= 12) {
            $val += 12;
        }
        else {
            $val += $est;
        }
    }
    return chr $val;
}


sub completion {
    my($shell) = @_;

    my $completion = '';
    my $seen = 0;
    while (my $line = <DATA>) {
        if ($line =~ /^__(\w+)__\Z/) {
            my $SHELL = lc $1;
            $seen = $shell eq $SHELL;
            next;
        }
        if ($seen) {
            $completion .= $line;
        }
    }

    return $completion;
}


#### helpers

sub get_json {
    my($jira_id, $json) = @_;

    say STDERR $json if $verbose;
    my $data = eval { decode_json($json) };
    if ($@) {
        output_error(sprintf "Error decoding JSON for %s: %s", $jira_id, $@);
        return;
    }
    return $data;
}


sub res_error {
    my($jira_id, $res, $what, $opts) = @_;

    say STDERR Dumper $res if $verbose;

    unless ($res->is_success) {
        unless ($opts->{no_error}) {
            my $error = $res->status_line;
            my $data = get_json($jira_id, $res->content);

            if ($data && keys %$data && $data->{errorMessages}) {
                $error .= ': ' . join("; ", @{ $data->{errorMessages} });
            }

            output_error(sprintf "Error %s for %s: %s", $what, $jira_id, $error);
        }
        return 1;
    }

    return 0;
}


sub req_headers {
    (
        'Authorization' => 'Bearer ' . $jira_token,
        'Content-Type'  => 'application/json'
    )
}


sub transitions_url {
    my($jira_id) = @_;
    issue_url($jira_id) . '/transitions'
}


sub issue_url {
    my($jira_id) = @_;
    $issue_url . $jira_id
}


sub workflow_dispatch_url {
    my($jira_id, $transition_id) = @_;
    my $issue = get_json($jira_id, get_issue($jira_id));
    "https://$host/secure/CommentAssignIssue!default.jspa?id=$issue->{id}&action=$transition_id";
}

sub html_url {
    my($jira_id) = @_;
    "https://$host/browse/" . $jira_id
}


sub output_error {
    my(@lines) = @_;
    say STDERR for @lines;
    $err = -1;
}


sub read_cache {
    my($type, $jira_id) = @_;
    my $file = "$jira_cache$jira_id.$type";
    return if -e $file && -M _ > 1/48;
    open my $fh, '<', $file or return;
    return join '', <$fh>;
}


sub write_cache {
    my($type, $jira_id, $json) = @_;
    mkpath $jira_cache;
    my $file = "$jira_cache$jira_id.$type";
    open my $fh, '>', "$jira_cache$jira_id.$type";
    print $fh $json;
}


sub jira_id {
    my($id) = @_;

    if (length $id) {
        return _jira_id($id);
    }
    else {
        return _jira_id(`git rev-parse --abbrev-ref HEAD 2>&1`);
    }
}

sub _jira_id {
    my $id = shift;
    return unless defined $id && length $id;
    chomp $id;
    return uc($1) if $id =~ m|(\w+-\d+)|;
    return;
}


#### init

sub init {
    Getopt::Long::Configure('bundling');
    GetOptions(
        'h|help',                sub { pod2usage(-verbose => 2) },
        'V|version',             \$version,
        'v|verbose',             \$verbose,
    ) or pod2usage(-verbose => 1);

    if ($version) {
        printf "$0 version v%s\n", $VERSION;
        return;
    }

    my $cmd = shift @ARGV;

    pod2usage(-verbose => 1, -message => "No command found") unless $cmd;

    my $all_cmds = qr/^(
         jira_id|completion|test
        |status_line|short_status|status
        |transition|get_transitions|list_transitions
        |estimate|short_estimate
        |get_issue|refresh
    )$/x;

    if ($cmd !~ $all_cmds) {
        pod2usage(-verbose => 1, -message => "Command '$cmd' not recognized");
    }

    $jira_cache = $ENV{JIRA_CACHE} //= "$ENV{HOME}/.jira_cache/";
    $jira_token = $ENV{JIRA_TOKEN} //= `cat $ENV{HOME}/.jira_token`;
    $ua = LWP::UserAgent->new;

    my($jira_id);

    if ($cmd eq 'transition') {
        my $transition = shift @ARGV;
        $jira_id = jira_id(shift(@ARGV));
        output_error('No Jira ID') and return unless $jira_id;
        push @args, $jira_id;
        push @args, $transition if defined $transition;
    }
    elsif ($cmd eq 'estimate') {
        my $minutes;
        # 1. estimate # get
        # 2. estimate ID # get
        # 3. estimate MINUTES # set
        # 4. estimate MINUTES ID # set
        if (@ARGV) {
            $minutes = shift(@ARGV);
            if ($minutes =~ /^\d+$/) { # 3
                $jira_id = jira_id(shift(@ARGV)); # 4
            }
            else {
                $jira_id = jira_id($minutes); # 2
                undef $minutes;
            }
        }
        else {
            $jira_id = jira_id(); # 1
        }
        output_error('No Jira ID') and return unless $jira_id;
        push @args, $jira_id;
        push @args, $minutes if defined $minutes;
    }
    elsif ($cmd eq 'completion') {
        my $completion_shell = shift @ARGV;
        push @args, $completion_shell;
    }
    else {
        $jira_id = jira_id(shift(@ARGV));
        push @args, $jira_id;
    }

    {
        if ($cmd eq 'list_transitions') {
            $sub = \&_list_transitions;
        }
        else {
            no strict 'refs';
            $sub = \&{$cmd};
        }
    }

    return 1;
}


__END__
__BASH__
#!/bin/bash

_transitions() {
    local IFS=$'\n'
    local cur=${COMP_WORDS[COMP_CWORD]}
    local prev=${COMP_WORDS[COMP_CWORD-1]}
    local count=${#COMP_WORDS[@]}
    local word_list
    local candidates

    COMPREPLY=()

    if [[ $count -eq 2 ]]; then
        word_list=(status status_line short_status transition list_transitions completion get_transitions get_issue refresh estimate short_estimate)
    elif [[ $count -eq 3 && $prev == 'transition' ]]; then
        word_list=($(jira_status list_transitions | jq -r .[]))
    elif [[ $count -eq 3 && $prev == 'completion' ]]; then
        word_list=(bash)
    fi

    candidates=($(compgen -W "${word_list[*]}" -- "$cur"))

    if [ ${#candidates[*]} -eq 0 ]; then
        COMPREPLY=()
    else
        COMPREPLY=($(printf '%q\n' "${candidates[@]}"))
    fi
}

complete -F _transitions jira_status
