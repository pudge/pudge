#!/usr/bin/perl
use strict;
use warnings;

use Getopt::Long;

my $cmd;
Getopt::Long::Configure('bundling');
GetOptions(
    'c|cmd=s' => \$cmd,
) or die "Cannot parse options";
$cmd = $cmd ? " \Q$cmd\E" : "";

our @hosts = @ARGV;

if ($ENV{TMUX} || $ENV{TMUX_PANE}) {
    die "Already inside a tmux session, do not know what to do.\n";
}

use File::Temp 'tempfile';
my($fh, $filename) = tempfile();

my $session_name = "session-$$";
print $fh qq{tmux new-session -d -s "$session_name"\n};

my $layout = @hosts > 5 ? 'tiled' : 'even-vertical';

my $i = 0;
for my $host (@hosts) {
    if ($i++) {
        print $fh qq{tmux split-window -t "$session_name"\n};
        print $fh qq{tmux select-layout -t "$session_name" tiled\n};
    }
    print $fh qq{tmux send-keys -t "$session_name" 'ssh $host$cmd' Enter\n};
}
print $fh qq{tmux select-layout -t "$session_name" $layout\n};
print $fh qq{tmux set-window -t "$session_name" synchronize-panes on\n};
print $fh qq{tmux select-pane -t "$session_name" -R\n};
close $fh;
system("bash $filename >/dev/null");

printf STDOUT "\e]1;%s\a", $session_name;
system(qq{tmux attach -t "$session_name"});
